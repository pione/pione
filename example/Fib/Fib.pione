param $NUM := 3

Rule Main
  output 'result.txt'
Flow
  rule Fib {N: $NUM}
  rule Result {N: $NUM}
End

Rule Fib0
  output 'fib0.txt'
Action
  echo -n '0' > fib0.txt
End

Rule Fib1
  output 'fib1.txt'
Action
  echo -n '1' > fib1.txt
End

Rule Fib
  output 'fib{$N}.txt'
  param $N
Flow
  case $N
  when 0
    rule Fib0
  when 1
    rule Fib1
  else
    $P1 := $N - 2
    $P2 := $N - 1
    rule Fib {N: $P1}
    rule Fib {N: $P2}
    rule Calc {N: $N, P1: $P1, P2: $P2}
  end
End

Rule Calc
  input 'fib{$P1}.txt'
  input 'fib{$P2}.txt'
  output 'fib{$N}.txt'
  param $N
  param $P1
  param $P2
Action
  expr `cat {$I[1]}` + `cat {$I[2]}` > {$O[1]}
End

Rule Result
  input 'fib{$N}.txt'
  output 'result.txt'
  param $N
Action
  cat {$I[1]} > {$O[1]}
End
