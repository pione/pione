param $MAX := 10

Rule Main
  output '*.prime'.all
Flow
  rule CreateUndeterminedNumbers >>> Sieve
End

Rule CreateUndeterminedNumbers
  output '*.ud'.all
  param $N := 2.upto($MAX)
Flow
  rule CreateData {NAME: "{$N}.ud"}
End

Rule Sieve
  input '*.ud'.all or null
  input '*.prime'.all or null
  output '*.prime'.all.neglect
  output 'finished'
  param $N := 2
Flow
  if $*.as_data_expr.match?($N.str)
    if ($N * $N) <= $MAX
      rule CreateData {NAME: "{$N}.prime"} >>> RemoveUndeterminedNumber >>> Sieve {N: $N+1}
    else
      rule UndeterminedNumber2Prime
      rule CreateData {NAME: "finished"}
    end
  else
    rule Sieve {N: $N+1}
  end
End

Rule RemoveUndeterminedNumber
  input '*.prime'
  input '*.ud'
  output '{$I[2][1]}.ud'.remove
  constraint ($I[2][1].i % $I[1][1].i) == 0
End

Rule UndeterminedNumber2Prime
  input '*.ud'
  output '{$*}.prime'.touch
End

Rule CreateData
  param $NAME
  output '{$NAME}'.touch
End
